Open_Big_Menu_Loading_VBlank:
	LD A, $mem_bigmenu_loading_flag
	AND $01
	JR Z, =Big_Menu_Fade_out
	JP =Load_Big_Menu

Big_Menu_Fade_out:
	; Fading Out Animation
	LD B, $00
	LD A, $mem_prepared_color_palette
	SLA A
	SLA A
	LD $mem_prepared_color_palette, A
	OR B
	LD B, A

	LD A, $reg_obj1_palette
	SLA A
	SLA A
	LD $reg_obj1_palette, A
	OR B
	LD B, A

	LD A, $reg_obj0_palette
	SLA A
	SLA A
	LD $reg_obj0_palette, A
	OR B
	LD B, A

	LD A, $mem_gui_text_bold_palette
	SLA A
	SLA A
	LD $mem_gui_text_bold_palette, A
	OR B
	LD B, A

	LD A, $mem_gui_text_thin_palette
	SLA A
	SLA A
	LD $reg_bg_palette, A
	LD $mem_gui_text_thin_palette, A
	OR B

	CP $00
	RET NZ

	LD A, $mem_bigmenu_loading_flag
	OR $01
	LD $mem_bigmenu_loading_flag, A

	RET

Set_bigmenu_window_tilemap_row: ; row in HL
	.bottom:
		LD A, $11
		JR =.write_row
	.top:
		LD A, $10
	.write_row:
		LD (HL+), A
		ADD $06
		LD (HL+), A
		LD (HL+), A
		LD (HL+), A
		LD (HL+), A
		LD (HL+), A
		SUB $04
		LD (HL+), A
		LD BC, $19
		ADD HL, BC
		RET

	.middle:
		LD A, $14
		LD (HL+), A
		XOR A
		LD (HL+), A
		LD (HL+), A
		LD (HL+), A
		LD (HL+), A
		LD (HL+), A
		LD A, $15
		LD (HL+), A
		LD BC, $19
		ADD HL, BC
		RET

Load_Big_Menu_Window:
	LD HL, $9800
	CALL =Set_bigmenu_window_tilemap_row.top
	CALL =Set_bigmenu_window_tilemap_row.middle
	CALL =Set_bigmenu_window_tilemap_row.middle
	CALL =Set_bigmenu_window_tilemap_row.middle
	CALL =Set_bigmenu_window_tilemap_row.bottom
	CALL =Set_bigmenu_window_tilemap_row.top
	LD A, $16
	LD ($98a0), A
	CALL =Set_bigmenu_window_tilemap_row.middle
	XOR A
	LD ($98c0), A
	CALL =Set_bigmenu_window_tilemap_row.bottom
	LD A, $17
	LD ($98e0), A

	LD HL, $9821
	LD A, $f2
	LD (HL+), A
	LD A, $a2
	LD (HL+), A
	LD A, $8e
	LD (HL+), A
	LD A, $9c
	LD (HL+), A
	LD HL, $9862
	LD A, $97
	LD (HL+), A
	LD A, $98
	LD (HL+), A
	RET

Load_Big_Menu:
	DI
	.ENABLE_MODE_2_INTERRUPT
	EI

	LD A, $38
	LD $reg_viewport_x, A
	XOR A
	LD $reg_viewport_y, A
	LD A, $50
	LD $reg_window_y, A

	LD HL, $9807
	LD C, $00
	.set_tilemap_row_loop:
		LD A, C
		CP $00
		JP Z, =.frame_tileline_start
		CP $03
		JP Z, =.frame_tileline_start
		CP $02
		JP Z, =.frame_tileline_end
		CP $05
		JP Z, =.frame_tileline_end

		JP =.frame_tileline_middle
		.set_tile:
		LD (HL), B
		INC HL
		LD A, L
		AND $1f
		CP $1b
		JR NZ, =.set_tilemap_row_loop
		INC C
		LD A, C
		CP $16
		JR NZ, =.set_tilemap_row_loop

	CALL =Load_Big_Menu_Window

	CALL =.Display_Menu_Entries

	; Up and Down arrows
	LD A, $mem_bigmenu_entries_scroll
	AND A
	JR Z, =.skip_up_arrow

	LD A, $1c
	LD ($980a), A
	LD A, $1d
	LD ($980b), A
	.skip_up_arrow:

	LD A, $mem_bigmenu_cursor_max
	CP $07
	JR NZ, =.skip_down_arrow

	LD A, $1e
	LD ($984a), A
	LD A, $1f
	LD ($984b), A
	.skip_down_arrow:

	LD HL, $9889
	.LOAD_BANK_OF =Dungeon_Selection_Bottom_Txt
	LD BC, ptr(=Dungeon_Selection_Bottom_Txt)
	CALL =Print_str

	LD A, $lcdc_bigmenu_tilemap
	LD $reg_lcd_controller, A

	XOR A
	LD $mem_bigmenu_cursor_position, A
	CALL =Change_Big_Menu_Cursor_Position

	LD A, $enum_bigmenu_mode
	LD $mem_current_mode, A
	LD $mem_requested_mode, A

	DI
	.DISABLE_MODE_2_INTERRUPT
	CALL =Update_VBlank_Handler
	.ENABLE_LYC_INTERRUPT =Big_Menu_STAT_Entrypoint
	.ENABLE_VBLANK_INTERRUPTS
	.RESET_INTERRUPTS

	EI
	JP =Wait_for_Interrupt.loop
	RET

	.frame_tileline_end:
		LD A, L
		AND $1f
		CP $07
		JR Z, =.last_line_start
		CP $1a
		JR Z, =.last_line_end
		LD B, $17
		JP =.set_tile

		.last_line_start:
		LD B, $11
		JP =.set_tile

		.last_line_end:
		LD B, $13
		JP =.set_tile

	.frame_tileline_start:
		LD A, L
		AND $1f
		CP $07
		JR Z, =.first_line_start
		CP $1a
		JR Z, =.first_line_end
		LD B, $16
		JP =.set_tile

		.first_line_start:
		LD B, $10
		JP =.set_tile

		.first_line_end:
		LD B, $12
		JP =.set_tile

	.frame_tileline_middle:
		LD A, L
		AND $1f
		CP $07
		JR Z, =.middle_line_start
		CP $1a
		JR Z, =.middle_line_end
		LD B, $00
		JP =.set_tile

		.middle_line_start:
		LD B, $14
		JP =.set_tile

		.middle_line_end:
		LD B, $15
		JP =.set_tile

	.Display_Menu_Entries:
		LD A, $07
		LD $mem_bigmenu_cursor_max, A
		LD D, $00

		.display_entries_lines.loop:
			LD H, $00
			LD L, D
			SWAP L
			ADD HL, HL
			ADD HL, HL

			LD A, D
			LD DE, $98e9
			ADD HL, DE
			LD D, A

			CALL =Load_Entry

			LD A, $mem_bigmenu_cursor_max
			CP $06
			JR Z, =.display_entries_lines.end

			INC D
			LD A, D
			CP $07
			JR NZ, =.display_entries_lines.loop

			LD A, (HL)
			INC A
			JR Z, =.display_entries_lines.end

		.display_entries_lines.end:
		LD A, D
		LD $mem_bigmenu_cursor_max, A

		RET

Change_Big_Menu_Cursor_Position: ; ONLY DURING VBLANK, takes new cursor position in A, breaks C and HL
		LD C, A
		LD A, $mem_bigmenu_cursor_position
		CALL =.HL_from_cursor_position
		LD (HL), $00
		LD A, C
		LD $mem_bigmenu_cursor_position, A
		CALL =.HL_from_cursor_position
		LD (HL), $f2
		RET

		.HL_from_cursor_position:
			; Shift left by 6
			LD H, A
			SRL H
			SRL H
			AND 0b11
			SWAP A
			CP A
			RLA
			RLA
			ADD $e8
			LD L, A
			LD A, H
			ADC $98
			LD H, A
			RET

Load_Previous_Entry:
	LD A, $mem_bigmenu_entries_scroll
	CP $00
	RET Z
	SUB $01
	AND $07
	LD H, $00
	LD L, A
	ADD HL, HL
	ADD HL, HL
	ADD HL, HL
	ADD HL, HL
	ADD HL, HL
	ADD HL, HL
	LD BC, $98e9
	ADD HL, BC
	LD A, $mem_bigmenu_entries_scroll
	SUB $01
	JR =Load_Entry

Load_Next_Entry:
	LD A, $mem_bigmenu_entries_scroll
	ADD $06
	AND $07
	LD H, $00
	LD L, A
	ADD HL, HL
	ADD HL, HL
	ADD HL, HL
	ADD HL, HL
	ADD HL, HL
	ADD HL, HL
	LD BC, $98e9
	ADD HL, BC
	LD A, $mem_bigmenu_entries_scroll
	ADD $06

Load_Entry:
; HL = memory_position_shenanigan_thingy
; A = scroll position of the entry
; BREAKS BC
	PUSH HL

	LD L, A
	LD H, $00

	ADD HL, HL
	ADD HL, HL
	ADD HL, HL

	LD A, ($mem_bigmenu_entries+1)
	LD B, A
	LD A, ($mem_bigmenu_entries+2)
	LD C, A
	ADD HL, BC

	LD A, ($mem_bigmenu_entries)
	.CHANGE_BANK_TO_A

	LD A, (HL)
	CP $ff
	JR Z, =.no_more_entries

	INC HL
	LD B, (HL)
	INC HL
	LD C, (HL)

	.CHANGE_BANK_TO_A

	POP HL

	CALL =Print_str

	.loop:
		LD A, L
		AND $1f
		CP $19
		JR Z =.end
		XOR A
		LD (HL+), A
		JR =.loop
	.end:

	RET
	
	.no_more_entries:
	POP HL
	LD A, $06
	LD $mem_bigmenu_cursor_max, A
	LD A, $17
	LD ($984a), A
	LD ($984b), A
	RET

Big_Menu_Check_Cursor_Move:
	.main:
	LD A, $mem_button_direction
	CP $00
	RET Z
	LD C, A
	LD A, $mem_last_button_direction
	CP C
	RET Z
	LD A, C
	CP $enum_direction_down
	JR Z, =.down
	CP $enum_direction_up
	JR Z, =.up
	RET

	.down:
		LD A, $mem_bigmenu_loading_flag
		AND $20
		JP NZ, =.confirm_window

		LD A, $mem_bigmenu_cursor_max
		LD C, A
		LD A, $mem_bigmenu_cursor_position
		LD B, A
		LD A, $mem_bigmenu_entries_scroll
		CPL
		INC A
		INC B
		ADD B
		AND $07
		CP C
		JR Z, =.cancel_move_cursor
		CP $06
		CALL Z, =.scroll_down
		LD A, B
		AND $07
		.SET_SOUND_EFFECT =_sound_effect_Click
		JP =Change_Big_Menu_Cursor_Position

		.cancel_move_cursor:
		.SET_SOUND_EFFECT =_sound_effect_Move_arrow_failed
		RET

		.scroll_down:
		LD A, $mem_bigmenu_entries_scroll
		INC A
		LD $mem_bigmenu_entries_scroll, A
		LD A, $1c
		LD ($980a), A
		LD A, $1d
		LD ($980b), A
		PUSH BC
		CALL =Load_Next_Entry
		POP BC
		LD A, B
		CP $08
		RET NZ
		LD B, $00
		RET

	.up:
		LD A, $mem_bigmenu_loading_flag
		AND $20
		JR NZ, =.confirm_window

		LD A, $mem_bigmenu_cursor_position
		LD B, A
		LD A, $mem_bigmenu_entries_scroll
		CPL
		INC A
		ADD B
		AND $07

		CP $00
		CALL Z, =.scroll_up
		LD A, B
		DEC A
		AND $07
		.SET_SOUND_EFFECT =_sound_effect_Click
		JP =Change_Big_Menu_Cursor_Position

		.scroll_up:
			LD A, $mem_bigmenu_entries_scroll
			CP $00
			JR Z, =.up.skip_cursor_position_change


			LD C, A
			LD A, $1e
			LD ($984a), A
			LD A, $1f
			LD ($984b), A
			LD A, $07
			LD $mem_bigmenu_cursor_max, A
			DEC C
			JR NZ, =.up.skip_reset_arrow_up
			LD A, $16
			LD ($980a), A
			LD ($980b), A
			.up.skip_reset_arrow_up:
			LD A, C
			LD $mem_bigmenu_entries_scroll, A
			PUSH BC
			CALL =Load_Previous_Entry
			POP BC
			RET
			.up.skip_cursor_position_change:
			ADD SP, $02
			JP =.cancel_move_cursor

	.confirm_window:
		.SET_SOUND_EFFECT =_sound_effect_Click
		LD A, $mem_bigmenu_loading_flag
		XOR $40
		LD $mem_bigmenu_loading_flag, A
		AND $40

		LD HL, $9821
		OR L
		LD L, A
		LD (HL), $f2
		XOR $40
		LD L, A
		LD (HL), $00
		RET

Big_Menu_Check_Confirm:
	LD A, $mem_button_action
	LD C, A
	LD A, $mem_last_button_action
	XOR C
	AND C
	BIT 0, A
	JR NZ, =.confirm
	BIT 1, A
	JR NZ, =.cancel
	RET

	.confirm:
	.SET_SOUND_EFFECT =_sound_effect_Click
	LD A, $mem_bigmenu_loading_flag
	BIT 5, A
	JR NZ, =.choose_confirm_option
	OR $20
	LD $mem_bigmenu_loading_flag, A
	LD A, $reg_lcd_controller
	OR $20
	LD $reg_lcd_controller, A
	LD HL, $9889
	.LOAD_BANK_OF =Dungeon_Selection_Confirm_Bottom_Txt
	LD BC, ptr(=Dungeon_Selection_Confirm_Bottom_Txt)
	CALL =Print_str
	XOR A
	LD (HL+), A
	LD (HL+), A
	LD (HL+), A
	RET

	.choose_confirm_option:
	LD A, $mem_bigmenu_loading_flag
	BIT 6, A
	JR NZ, =.cancel

	LD H, $00
	LD A, $mem_bigmenu_entries_scroll
	LD L, A
	LD A, $mem_bigmenu_cursor_position
	SUB L
	AND $07
	ADD L
	LD L, A

	ADD HL, HL
	ADD HL, HL
	ADD HL, HL

	LD A, ($mem_bigmenu_entries+1)
	LD B, A
	LD A, ($mem_bigmenu_entries+2)
	LD C, A
	LD A, ($mem_bigmenu_entries)
	.CHANGE_BANK_TO_A

	ADD HL, BC

	LD BC, $07
	ADD HL, BC

	LD E, (HL)
	DEC HL
	LD D, (HL)
	DEC HL

	LD C, (HL)
	DEC HL
	LD B, (HL)
	DEC HL

	LD A, (HL)
	.CHANGE_BANK_TO_A
	LD H, B
	LD L, C

	JP HL

	.cancel:
	.SET_SOUND_EFFECT =_sound_effect_Menu_Close
	LD A, $mem_bigmenu_loading_flag
	BIT 5, A
	JP Z, =Load_Back_Dungeon
	RES 5, A
	LD $mem_bigmenu_loading_flag, A
	LD A, $reg_lcd_controller
	RES 5, A
	LD $reg_lcd_controller, A
	LD HL, $9889
	.LOAD_BANK_OF =Dungeon_Selection_Bottom_Txt
	LD BC, ptr(=Dungeon_Selection_Bottom_Txt)
	JP =Print_str

Big_Menu_VBLANK_Entrypoint:
	LD A, $reg_interrupt_enable
	AND $fe
	LD $reg_interrupt_enable, A
	EI
	LD A, $palette_thin_font
	LD $reg_bg_palette, A

	CALL =Pad_Button_Check
	CALL =Big_Menu_Check_Cursor_Move
	CALL =Big_Menu_Check_Confirm

	CALL =Play_Music
	CALL =Play_Sound_Effect
	.ENABLE_VBLANK_INTERRUPTS
	LD A, $reg_lcd_status
	AND $03
	RETI

Big_Menu_STAT_Entrypoint:
	PUSH AF
	LD A, $reg_lyc
	CP $90
	JR Z, =.new_frame
	CP $07
	JR Z, =.choices_start
	CP $67
	JR Z, =.choices_end
	.HBLANK_WAIT
	LD A, $reg_lyc
	CPL
	INC A
	ADD $2f
	LD $reg_viewport_y, A
	LD A, $67
	LD $reg_lyc, A
	POP AF
	RETI

	.choices_start:
	.HBLANK_WAIT
	LD A, $mem_bigmenu_entries_scroll
	SWAP A
	AND $70
	ADD $28
	LD $reg_viewport_y, A
	LD A, $67
	LD $reg_lyc, A
	LD A, $mem_bigmenu_entries_scroll
	AND $07
	CP $03
	JR C, =.skip_set_choices_wrap_around
	LD A, $mem_bigmenu_entries_scroll
	SWAP A
	AND $70
	CPL
	INC A
	ADD $87
	LD $reg_lyc, A
	.skip_set_choices_wrap_around:
	POP AF
	RETI

	.choices_end:
	.HBLANK_WAIT
	LD A, $a0
	LD $reg_viewport_y, A
	LD A, $90
	LD $reg_lyc, A
	POP AF
	RETI

	.new_frame:
	XOR A
	LD $reg_viewport_y, A
	LD A, $07
	LD $reg_lyc, A
	POP AF
	RETI

; Menu choices struct $ff terminated array of:
; struct menu_entry (8 bytes) {
; 	name: u24 (bank+ptr)
;	function: u24 (bank+ptr)
;	padding/args: u16
; } (NO CROSS BANK ARRAY)

Open_Big_Menu:
	.SET_SOUND_EFFECT =_sound_effect_Menu_Open
	LD BC, =Open_Big_Menu_Loading_VBlank
	LD A, B
	LD $mem_loading_mode_vblank_func_pointer_high, A
	LD A, C
	LD $mem_loading_mode_vblank_func_pointer_low, A

	LD BC, =Nothing
	LD A, B
	LD $mem_loading_mode_regular_func_pointer_high, A
	LD A, C
	LD $mem_loading_mode_regular_func_pointer_low, A

	XOR A
	LD $mem_bigmenu_entries_scroll, A

	LD A, $enum_loading_mode
	LD $mem_requested_mode, A
	RET

Close_Big_Menu_Fade_Out_Interrupt:
	PUSH AF
	PUSH BC
	PUSH DE
	PUSH HL
	LD A, $saved_rom_bank
	PUSH AF
	CALL =Big_Menu_Fade_out
	CALL =Play_Music
	CALL =Play_Sound_Effect
	.ENABLE_VBLANK_INTERRUPTS
	.RESET_INTERRUPTS
	POP AF
	.CHANGE_BANK_TO_A
	POP HL
	POP DE
	POP BC
	POP AF
	RETI

Load_Back_Dungeon:
	DI
	.SET_VBLANK_INTERRUPT =Close_Big_Menu_Fade_Out_Interrupt
	EI

	XOR A
	LD $mem_bigmenu_entries_scroll, A
	LD $mem_bigmenu_cursor_max, A
	LD $mem_bigmenu_cursor_position, A

	LD A, $enum_dungeon_mode
	LD $mem_current_mode, A
	LD $mem_requested_mode, A

	LD HL, $mem_entities_list
	.BANK_CALL =Center_viewport_around_entity
	CALL =Load_Prepared_Map
	LD A, $mem_prepared_viewport_x
	LD $reg_viewport_x, A
	LD A, $mem_prepared_viewport_y
	LD $reg_viewport_y, A

	.CLOSE_DIALOGUE

	LD A, $mem_map_loading_flags
	SET 6, A
	LD $mem_map_loading_flags, A
	DI
	XOR A
	LD $mem_bigmenu_loading_flag, A
	JP =Dungeon_generation_VBlank
