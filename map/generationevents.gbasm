Generation_Event_Execution:
	LD A, $mem_floor_count
	LD D, A
	LD HL, $mem_dungeon_generation_events
	.loop:
		LD A, (HL+)
		AND A
		JR Z, =.skip
		CP D
		JR NZ, =.skip

		LD A, (HL+)
		PUSH HL
		PUSH DE
		LD BC, =Generation_Event_Jump_Table

		.JUMP_TABLE

		POP DE
		POP HL

		.skip:
		LD A, L
		AND $f8
		ADD $08
		LD L, A
		AND A
		JR NZ, =.loop
	RET

Generation_Event_Jump_Table:
	; 00
	RET
	NOP
	NOP
	NOP

	; 01
	JP =.Dialogue_Script
	NOP

	; 02
	JP =.Remove_Stairs
	NOP

	; 03
	JP =.Load_Entity
	NOP

	; 04
	JP =.Entity_Random_Room
	NOP

	; 05
	JP =.Entity_XY
	NOP

	; 06
	JP =.Set_Dialogue_Ptr
	NOP

	; 07
	JP =.Object_XY
	NOP

	; 08
	JP =.Object_Random_Room
	NOP

	; 09
	JP =.Add_Saved_Object_Parameter
	NOP

	; 0a
	JP =.Dialogue_Jump_table
	NOP

	; 0b
	JP =.Move_Entity_XY_direction
	NOP

	.Dialogue_Script:
		LD A, (HL+)
		LD $mem_dialogue_script_bank, A
		LD A, (HL+)
		LD ($mem_dialogue_script_program_counter), A
		LD A, (HL)
		LD ($mem_dialogue_script_program_counter+1), A
		JP =Dialogue_script_step

	.Load_Entity:
		LD A, (HL+)
		LD E, A
		LD D, $00
		LD A, (HL)
		LD HL, $mem_loaded_entities_indices
		ADD HL, DE
		LD (HL), A
		RET

	.Entity_Random_Room:
		LD A, (HL+)
		LD E, A
		SWAP E

		LD A, $01
		LD $tmp_var_3, A

		LD D, (HL)
		LD H, high($mem_entities_list)
		LD L, E
		LD A, D
		JP =Initialize_Entity

	.Entity_XY:
		LD A, (HL+)
		LD E, A
		SWAP E

		LD A, (HL+)
		LD D, A
		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A
		LD A, (HL)
		AND $0f
		SWAP A
		OR $02
		LD $tmp_var_3, A
		LD H, high($mem_entities_list)
		LD L, E
		LD A, D
		JP =Initialize_Entity

	.Set_Dialogue_Ptr:
		LD D, high($mem_entities_list)
		LD A, (HL+)
		LD E, A
		SWAP E
		LD A, $0b
		OR E
		LD E, A
		LD A, (HL+)
		LD (DE), A
		INC DE
		LD A, (HL+)
		LD (DE), A
		INC DE
		LD A, (HL)
		LD (DE), A
		RET

	.Object_XY:
		LD A, (HL+)
		LD D, A
		LD A, (HL+)
		LD E, A
		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A

		PUSH HL
		CALL =Find_Free_Object_slot

		LD A, D
		LD (HL+), A 
		LD A, B
		LD (HL+), A 
		LD A, C
		LD (HL+), A 
		LD A, E
		LD (HL+), A 

		LD A, L
		SWAP A
		RLCA
		AND $0f
		LD B, A
		POP HL

		LD A, (HL+)
		LD D, A
		CP $00
		RET Z
		LD A, (HL+)
		LD E, A
		LD A, B
		LD (DE), A

		RET

	.Object_Random_Room:
		LD A, (HL+)
		LD D, A
		LD A, (HL+)
		LD E, A

		PUSH HL
		CALL =Find_Free_Object_slot
		LD A, D
		PUSH HL
		CALL =Spawn_object_in_random_room_avoid_bunny
		POP HL

		LD A, L
		SWAP A
		RLCA
		AND $0f
		LD B, A
		POP HL

		LD A, (HL+)
		LD D, A
		CP $00
		RET Z
		LD A, (HL+)
		LD E, A
		LD A, B
		LD (DE), A

		RET

	.Add_Saved_Object_Parameter:
		LD A, (HL+)
		LD D, A
		LD A, (HL+)
		LD E, A
		LD A, (DE)
		SWAP A
		RRCA
		AND $f8
		OR $04
		LD E, A
		LD D, high($mem_object_list)

		LD A, (HL+)
		LD (DE), A
		INC DE
		LD A, (HL+)
		LD (DE), A
		INC DE
		LD A, (HL+)
		LD (DE), A
		INC DE
		LD A, (HL+)
		LD (DE), A
		RET

	.Remove_Stairs:
		LD HL, $mem_object_list
		LD (HL), $00
		RET

	.Dialogue_Jump_table:
		LD A, (HL+)
		.CHANGE_BANK_TO_A
		LD A, (HL+)
		LD D, A
		LD A, (HL+)
		LD E, A

		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A

		LD A, (BC)
		LD H, $00
		LD L, A
		LD B, $00
		LD C, A
		ADD HL, HL
		ADD HL, BC
		ADD HL, DE

		LD A, (HL+)
		LD $mem_dialogue_script_bank, A
		LD A, (HL+)
		LD ($mem_dialogue_script_program_counter), A
		LD A, (HL)
		LD ($mem_dialogue_script_program_counter+1), A
		JP =Dialogue_script_step

	.Move_Entity_XY_direction:
		LD D, high($mem_entities_list)
		LD A, (HL+)
		LD E, A
		SWAP E
		INC E
		LD A, (HL+)
		LD (DE), A
		INC DE
		LD A, (HL+)
		LD (DE), A
		INC DE
		LD A, (HL)
		LD (DE), A
		RET
