; struct dungeon
; {
; 	name: *char (u24=bank+ptr),
; 	flags: 0b00CmTSFM,
; 		M = whether or not this dungeon has a pregenerated tilemap (0 means that it's randomly generated)
; 		F = whether or not the floors are counted
; 		S = whether or not enemies can spawn
; 		T = whether or not there is a top bar shown
; 		m = whether or not you can open the attack menu
;		C = whether or not there is a custom function to be executed every frame
; 	tilemap: ptr (u24=bank+ptr)
;	custom_function: ptr (u24=bank+ptr)
;   music: ptr (u24=bank+ptr)
; 	generation_event_size: u8 (size of the generation_eents_structure
; 	generation_events: [x]struct (8 bytes) {
; 		...see definitions
; 	}
; 	max_floor: u8
; 	inverted_max_floor: u8 (1/max_floor)
; 	spawning_pattern: [8]struct (5 bytes){
; 		entities_idx: [4]u8
; 		spawn_frequencies: u8
;	}
; }

.MACRODEF GEN_EVENT_DIALOGUE floor =dialogue
	$start:
	.DB low($floor), $01
	.DB bank(=dialogue), high(ptr(=dialogue)), low(ptr(=dialogue))
	.PADTO =$start+8
.END

.MACRODEF GEN_EVENT_SPECIAL_ENTITY floor entity_instance_idx entity_blueprint_idx
	$load:
	.DB low($floor), $03, $07, low($entity_blueprint_idx)
	.PADTO =$load+8
	$spawn:
	.DB low($floor), $04, low($entity_instance_idx), $07
	.PADTO =$spawn+8
.END

.MACRODEF GEN_EVENT_LOAD_ENTITY floor loaded_entity_idx entity_blueprint_idx
	$load:
	.DB low($floor), $03, low($loaded_entity_idx), low($entity_blueprint_idx)
	.PADTO =$load+8
.END

.MACRODEF GEN_EVENT_SPAWN_LOADED_ENTITY_XY floor entity_instance_idx loaded_entity_idx arg_x arg_y arg_d
	$spawn:
	.DB low($floor), $05, low($entity_instance_idx), low($loaded_entity_idx), low($arg_x), low($arg_y), low($arg_d)
	.PADTO =$spawn+8
.END

.MACRODEF GEN_EVENT_ENTITY_SET_DIALOGUE floor entity_instance_idx =dialogue_ptr
	$spawn:
		.DB low($floor), $06, low($entity_instance_idx), bank(=dialogue_ptr), high(ptr(=dialogue_ptr)), low(ptr(=dialogue_ptr))
	.PADTO =$spawn+8
.END

.MACRODEF GEN_EVENT_REMOVE_STAIRS floor
	$start:
	.DB low($floor), $02
	.PADTO =$start+8
.END

.MACRODEF GEN_EVENT_OBJECT_XY floor tile_idx action_idx arg_x arg_y
	$start:
	.DB low($floor), $07, low($tile_idx), low($action_idx), low($arg_x), low($arg_y), $00, $00
	.PADTO =$start+8
.END

.MACRODEF GEN_EVENT_OBJECT_XY_SAVE_IDX floor tile_idx action_idx arg_x arg_y mem_idx_save
	$start:
	.DB low($floor), $07, low($tile_idx), low($action_idx), low($arg_x), low($arg_y)
	.DB $mem_idx_save
	.PADTO =$start+8
.END

.MACRODEF GEN_EVENT_OBJECT_RANDOM_ROOM_SAVE_IDX floor tile_idx action_idx mem_idx_save
$start:
	.DB low($floor), $08, low($tile_idx), low($action_idx)
	.DB $mem_idx_save
	.PADTO =$start+8
.END

.MACRODEF GEN_EVENT_ADD_SAVED_OBJECT_PARAMETER floor mem_idx_save arg1 arg2 arg3 arg4
$start:
	.DB low($floor), $09,
	.DB $mem_idx_save
	.DB low($arg1), low($arg2), low($arg3), low($arg4)
	.PADTO =$start+8
.END

.MACRODEF GEN_EVENT_DIALOGUE_JUMP_TABLE floor =table mem_table_idx
	$start:
	.DB low($floor), $0a
	.DB bank(=table)
	.DB ptr(=table)
	.DB $mem_table_idx
	.PADTO =$start+8
.END

.MACRODEF GEN_EVENT_MOVE_ENTITY_XY_DIRECTION floor entity_idx x y direction
	$start:
	.DB low($floor), $0b
	.DB low($entity_idx), low($x), low($y), low($direction)
	.PADTO =$start+8
.END

All_The_Dungeons:
.INCLUDE "map/dungeons/morningforest.gbasm"
.INCLUDE "map/dungeons/lonelygrove.gbasm"
.INCLUDE "map/dungeons/campsite.gbasm"
.ASSERT bank(.) bank(=All_The_Dungeons)

Big_Menu_Choice_Load_Dungeon:
	DI
	LD A, $mem_bigmenu_loading_flag
	RES 0, A
	LD $mem_bigmenu_loading_flag, A
	.SET_VBLANK_INTERRUPT =Close_Big_Menu_Fade_Out_Interrupt
	EI

	.wait_fadeout:
	LD A, $mem_bigmenu_loading_flag
	BIT 0, A
	JR Z, =.wait_fadeout
	DI
	XOR A
	LD $mem_bigmenu_loading_flag, A
	LD A, $palette_normal
	LD $mem_prepared_color_palette, A
	LD A, $obj_palette_normal
	LD $reg_obj0_palette, A
	LD A, $obj_palette_frozen
	LD $reg_obj1_palette, A
	LD A, $00
	LD $reg_interrupt_enable, A
	LD A, bank(=All_The_Dungeons)
	LD ($mem_dungeon), A
	LD A, D
	LD ($mem_dungeon+1), A
	LD A, E
	LD ($mem_dungeon+2), A
	JP =New_Dungeon

