Dialogue_script_step:
	.ASSERT bank(.) $00
	.PUSH_BANK

	LD HL, &$mem_map_loading_flags
	SET 7, (HL)

	LD A, $mem_dialogue_script_bank
	.CHANGE_BANK_TO_A
	LD A, ($mem_dialogue_script_program_counter)
	LD H, A
	LD A, ($mem_dialogue_script_program_counter+1)
	LD L, A

	.next:
	LD A, (HL+)

	LD BC, =Dialogue_script_instruction_Jump_Table

	.JUMP_TABLE

	LD A, H
	LD ($mem_dialogue_script_program_counter), A
	LD A, L
	LD ($mem_dialogue_script_program_counter+1), A

	.POP_BANK
	RET

Dialogue_script_instruction_Jump_Table:
	; 00
	JP =.End
	NOP

	; 01
	JP =.Text
	NOP

	; 02
	JP =.TextB
	NOP

	; 03
	JP =.Learn_Attack
	NOP

	; 04
	JP =.TextB_Indirect
	NOP

	; 05
	JP =.Learn_Attack_Return
	NOP

	; 06
	JP =.Wait
	NOP

	; 07
	JP =.Fadeout_Dark
	NOP

	; 08
	JP =.Fadein_Dark
	NOP

	; 09
	JP =.Close_Dialogue
	NOP

	; 0A
	JP =.Set_mem
	NOP

	; 0B
	JP =.Go_to_Dungeon
	NOP

	; 0C
	JP =.Set_Gameover_Dialogue
	NOP

	; 0D
	JP =.Cpy_mem
	NOP

	; 0E
	JP =.Set_mem_bit
	NOP

	; 0F
	JP =.Res_mem_bit
	NOP

	; 10
	JP =.Branch_mem_bit
	NOP

	; 11
	JP =.TextB_Timer
	NOP

	; 12
	JP =.Shaking
	NOP

	; 13
	JP =.Entity_Turn_Around
	NOP

	; 14
	JP =.Reset_Sound_Channels
	NOP

	; 15
	JP =.Play_Sound_Effect
	NOP

	; 16
	JP =.Next_Floor
	NOP

	; 17
	JP =.Spawn_Loaded_Entity_XY_direction
	NOP

	.End:
		LD A, $mem_map_loading_flags
		RES 7, A
		LD $mem_map_loading_flags, A
		JP =Exit_Menu

	.Text:
		LD A, $enum_dungeon_dialogue_mode
		LD $mem_requested_mode, A

		PUSH HL
		LD HL, $dialogue_first_line
		LD BC, $12
		CALL =bzero
		POP HL

		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A
		PUSH HL
		LD HL, $dialogue_first_line
		CALL =Print_str
		POP HL

		PUSH HL
		LD HL, $dialogue_third_line
		LD BC, $12
		CALL =bzero
		POP HL

		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A
		PUSH HL
		LD HL, $dialogue_third_line
		CALL =Print_str
		POP HL

		LD A, $mem_display_flag
		AND $c0 ; Keeping the object ones
		OR $05
		LD $mem_display_flag, A

		RET

	.TextB:
		LD A, $enum_dungeon_dialogue_mode
		LD $mem_requested_mode, A

		PUSH HL
		LD HL, $dialogue_first_line
		LD BC, $12
		CALL =bzero
		POP HL

		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A
		PUSH HL
		LD HL, $dialogue_first_line
		CALL =Print_str
		POP HL

		PUSH HL
		LD HL, $dialogue_third_line
		LD BC, $12
		CALL =bzero
		POP HL

		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A
		PUSH HL
		LD HL, $dialogue_third_line
		CALL =Print_str
		POP HL

		LD A, $mem_display_flag
		AND $c0 ; Keeping the object ones
		OR $07
		LD $mem_display_flag, A

		RET

	.TextB_Indirect:
		LD A, $enum_dungeon_dialogue_mode
		LD $mem_requested_mode, A

		PUSH HL
		LD HL, $dialogue_first_line
		LD BC, $12
		CALL =bzero
		POP HL

		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A

		LD A, $saved_rom_bank
		LD $tmp_var_1, A

		LD A, (BC)
		.CHANGE_BANK_TO_A
		INC BC

		PUSH DE
		LD A, (BC)
		LD D, A
		INC BC
		LD A, (BC)
		LD E, A
		LD B, D
		LD C, E
		POP DE

		PUSH HL
		LD HL, $dialogue_first_line
		CALL =Print_str
		POP HL

		LD A, $tmp_var_1
		.CHANGE_BANK_TO_A

		PUSH HL
		LD HL, $dialogue_third_line
		LD BC, $12
		CALL =bzero
		POP HL

		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A
		PUSH HL
		LD HL, $dialogue_third_line
		CALL =Print_str
		POP HL

		LD A, $mem_display_flag
		AND $c0 ; Keeping the object ones
		OR $07
		LD $mem_display_flag, A

		RET

	.Learn_Attack:
		LD A, (HL+)
		LD E, A

		LD B, $00
		LD C, A
		SLA C
		RL B
		SLA C
		RL B
		SLA C
		RL B
		LD A, C
		ADD low(=Attack_List)
		LD C, A
		LD A, B
		ADC high(=Attack_List)
		LD B, A

		; TODO: FIX BANK SHENANIGANS

		LD A, (BC)
		LD ($mem_learn_attack_attack_name_ptr), A
		INC BC
		LD A, (BC)
		LD ($mem_learn_attack_attack_name_ptr+1), A
		INC BC
		LD A, (BC)
		LD ($mem_learn_attack_attack_name_ptr+2), A

		LD A, $mem_dialogue_script_bank
		LD ($mem_learn_attack_dialogue_ret_ptr), A
		LD A, H
		LD ($mem_learn_attack_dialogue_ret_ptr+1), A
		LD A, L
		LD ($mem_learn_attack_dialogue_ret_ptr+2), A

		CALL =Check_attack_already_learnt
		CP $01
		JR Z, =.Learn_Attack.Duplicate

		LD A, $mem_number_of_attacks
		CP $04
		JR NC, =.Learn_Attack.Not_enough_slot

		.Learn_Attack.Success:
			LD A, $mem_number_of_attacks
			LD HL, $mem_bunny_attacks
			ADD L
			LD L, A
			LD (HL), E
			LD A, $mem_number_of_attacks
			INC A
			LD $mem_number_of_attacks, A

			LD HL, ptr(=Learn_Attack_Dialogue_Script)
			LD A, bank(=Learn_Attack_Dialogue_Script)
			LD $mem_dialogue_script_bank, A
			.CHANGE_BANK_TO_A
			LD A, H
			LD ($mem_dialogue_script_program_counter), A
			LD A, L
			LD ($mem_dialogue_script_program_counter+1), A

			ADD SP, 2
			JP =Dialogue_script_step.next

		.Learn_Attack.Not_enough_slot:
			LD HL, ptr(=Learn_Attack_Not_Enough_Slot_Dialogue_Script)
			LD A, bank(=Learn_Attack_Not_Enough_Slot_Dialogue_Script)
			LD $mem_dialogue_script_bank, A
			.CHANGE_BANK_TO_A
			LD A, H
			LD ($mem_dialogue_script_program_counter), A
			LD A, L
			LD ($mem_dialogue_script_program_counter+1), A

			ADD SP, 2
			JP =Dialogue_script_step.next

		.Learn_Attack.Duplicate:
			LD HL, ptr(=Learn_Attack_Duplicate_Dialogue_Script)
			LD A, bank(=Learn_Attack_Duplicate_Dialogue_Script)
			LD $mem_dialogue_script_bank, A
			.CHANGE_BANK_TO_A
			LD A, H
			LD ($mem_dialogue_script_program_counter), A
			LD A, L
			LD ($mem_dialogue_script_program_counter+1), A

			ADD SP, 2
			JP =Dialogue_script_step.next

	.Learn_Attack_Return:
		LD A, ($mem_learn_attack_dialogue_ret_ptr)
		LD $mem_dialogue_script_bank, A
		.CHANGE_BANK_TO_A
		LD A, ($mem_learn_attack_dialogue_ret_ptr+1)
		LD ($mem_dialogue_script_program_counter), A
		LD H, A
		LD A, ($mem_learn_attack_dialogue_ret_ptr+2)
		LD ($mem_dialogue_script_program_counter+1), A
		LD L, A
		ADD SP, 2
		JP =Dialogue_script_step.next

	.Wait:
		LD A, $enum_animation_wait_mode
		LD $mem_requested_mode, A

		LD A, (HL+)
		LD $mem_animation_wait_frames, A

		LD A, $mem_map_loading_flags
		SET 7, A
		LD $mem_map_loading_flags, A
		RET

	.Fadeout_Dark:
		LD A, high(=Fade_out_Loading_VBlank)
		LD $mem_loading_mode_vblank_func_pointer_high, A
		LD A, low(=Fade_out_Loading_VBlank)
		LD $mem_loading_mode_vblank_func_pointer_low, A
		LD A, high(=Nothing)
		LD $mem_loading_mode_regular_func_pointer_high, A
		LD A, low(=Nothing)
		LD $mem_loading_mode_regular_func_pointer_low, A

		LD A, $enum_loading_mode
		LD $mem_requested_mode, A

		LD A, $mem_map_loading_flags
		SET 7, A
		LD $mem_map_loading_flags, A

		RET

	.Close_Dialogue:
		PUSH HL
		.CLOSE_DIALOGUE
		POP HL
		ADD SP, 2
		JP =Dialogue_script_step.next

	.Fadein_Dark:
		LD A, high(=Fade_in_Loading_VBlank)
		LD $mem_loading_mode_vblank_func_pointer_high, A
		LD A, low(=Fade_in_Loading_VBlank)
		LD $mem_loading_mode_vblank_func_pointer_low, A
		LD A, high(=Nothing)
		LD $mem_loading_mode_regular_func_pointer_high, A
		LD A, low(=Nothing)
		LD $mem_loading_mode_regular_func_pointer_low, A

		LD A, $enum_loading_mode
		LD $mem_requested_mode, A

		LD A, $mem_map_loading_flags
		SET 7, A
		LD $mem_map_loading_flags, A

		RET

	.Set_mem:
		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A
		LD A, (HL+)
		LD (BC), A
		ADD SP, 2
		JP =Dialogue_script_step.next

	.Go_to_Dungeon:
		LD A, (HL+)
		LD ($mem_dungeon), A
		LD A, (HL+)
		LD ($mem_dungeon+1), A
		LD A, (HL+)
		LD ($mem_dungeon+2), A

		LD A, $mem_sound_flags
		BIT 1, A
		CALL NZ, =Reset_Music_Channel1
		LD A, $mem_sound_flags
		BIT 2, A
		CALL NZ, =Reset_Music_Channel2
		LD A, $mem_sound_flags
		BIT 3, A
		CALL NZ, =Reset_Music_Channel3
		LD A, $mem_sound_flags
		BIT 4, A
		CALL NZ, =Reset_Music_Channel4
		LD A, $mem_sound_flags
		AND 0b11000001
		LD $mem_sound_flags, A
		LD A, $00
		LD $mem_sound_effect_frame_count, A
		LD A, $ff
		LD ($mem_sound_effect_pc), A

		; .SET_SOUND_EFFECT =_sound_effect_Next_Floor

		JP =New_Dungeon

	.Set_Gameover_Dialogue:
		LD A, (HL+)
		LD ($mem_gameover_dialogue_address), A
		LD A, (HL+)
		LD ($mem_gameover_dialogue_address+1), A
		LD A, (HL+)
		LD ($mem_gameover_dialogue_address+2), A
		ADD SP, 2
		JP =Dialogue_script_step.next

	.Cpy_mem:
		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A

		LD A, (HL+)
		LD D, A
		LD A, (HL+)
		LD E, A

		LD A, (DE)
		LD (BC), A

		ADD SP, 2
		JP =Dialogue_script_step.next

	.Set_mem_bit:
		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A
		LD A, (HL+)
		LD E, A
		LD D, $00
		LD A, (DE)
		LD E, A
		LD A, (BC)
		OR E
		LD (BC), A
		ADD SP, 2
		JP =Dialogue_script_step.next

	.Res_mem_bit:
		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A
		LD A, (HL+)
		LD E, A
		LD D, $00
		LD A, (DE)
		CPL
		LD E, A
		LD A, (BC)
		AND E
		LD (BC), A
		ADD SP, 2
		JP =Dialogue_script_step.next

	.Branch_mem_bit:
		ADD SP, 2
		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A
		LD A, (HL+)
		LD E, A
		LD D, $00
		LD A, (DE)
		LD E, A
		LD A, (BC)
		AND E
		LD A, (HL+)
		LD E, A
		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A
		JP Z, =Dialogue_script_step.next

		LD A, E
		LD $mem_dialogue_script_bank, A
		.CHANGE_BANK_TO_A
		LD A, B
		LD H, A
		LD ($mem_dialogue_script_program_counter), A
		LD A, C
		LD L, A
		LD ($mem_dialogue_script_program_counter+1), A
		JP =Dialogue_script_step.next

	.TextB_Timer:
		LD A, $mem_display_flag_2
		OR $08
		LD $mem_display_flag_2, A

		JP =.TextB

	.Shaking:
		LD A, (HL+)
		LD $mem_shaking_animation_level, A

		LD A, (HL+)
		LD $mem_animation_wait_frames, A

		LD A, high(=Shaking_Loading_VBlank)
		LD $mem_loading_mode_vblank_func_pointer_high, A
		LD A, low(=Shaking_Loading_VBlank)
		LD $mem_loading_mode_vblank_func_pointer_low, A
		LD A, high(=Animation_Wait_Mode.as_loading_regular)
		LD $mem_loading_mode_regular_func_pointer_high, A
		LD A, low(=Animation_Wait_Mode.as_loading_regular)
		LD $mem_loading_mode_regular_func_pointer_low, A

		LD A, $enum_loading_mode
		LD $mem_requested_mode, A

		LD A, $mem_map_loading_flags
		SET 7, A
		LD $mem_map_loading_flags, A
		RET
	
	.Entity_Turn_Around:
		LD A, (HL+)
		SWAP A
		ADD $03
		PUSH HL
		LD L, A
		LD H, high($mem_entities_list)

		LD A, (HL)
		AND $70
		SWAP A
		DEC A
		XOR $01
		INC A
		SWAP A
		LD B, A
		LD A, $8f
		AND (HL)
		OR B
		LD (HL), A

		POP HL
		ADD SP, 2
		JP =Dialogue_script_step.next
	
	.Reset_Sound_Channels:
		CALL =Reset_Music_Channels
		.CLEAR_SOUND_EFFECT
		ADD SP, 2
		JP =Dialogue_script_step.next
	
	.Play_Sound_Effect:
		.CLEAR_SOUND_EFFECT

		LD A, (HL+)
		LD ($mem_sound_effect_pc), A
		LD A, (HL+)
		LD ($mem_sound_effect_pc+1), A
		LD A, (HL+)
		LD ($mem_sound_effect_pc+2), A
		ADD SP, 2
		JP =Dialogue_script_step.next
	
	.Next_Floor:
		JP =Stairs_Action

	.Spawn_Loaded_Entity_XY_direction:
		LD A, (HL+)
		LD E, A
		SWAP E

		LD A, (HL+)
		LD D, A
		LD A, (HL+)
		LD B, A
		LD A, (HL+)
		LD C, A
		LD A, (HL+)
		AND $0f
		SWAP A
		OR $02
		LD $tmp_var_3, A
		.PUSH_BANK
		PUSH HL
		LD H, high($mem_entities_list)
		LD L, E
		LD A, D
		CALL =Initialize_Entity
		POP HL
		.POP_BANK
		ADD SP, 2
		JP =Dialogue_script_step.next
